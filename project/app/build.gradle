apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'realm-android'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "org.owntracks.android"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1100
        versionName "1.1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            proguardFiles '.proguard-project.txt'
            applicationIdSuffix '.debug'
            minifyEnabled false
        }

    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        main {
            jniLibs.srcDir 'jniLibs'
            java.srcDirs = ['src/main/java', 'build/generated/source/greendao/org/owntracks/android/db']
        }
    }

    signingConfigs {
        debug {
            keyAlias 'debug.owntracks.org'
            keyPassword '__DEBUG__'
            storeFile file('../debug-keystore.jks')
            storePassword '__DEBUG__'
        }
    }
    packagingOptions {
        // Remove unneeded Realm native libs (nearly no devices use these)
        exclude 'lib/armeabi/librealm-jni.so'
        exclude 'lib/mips/librealm-jni.so'
        exclude 'lib/x86/librealm-jni.so'
        exclude 'lib/x86_64/librealm-jni.so'
    }


}

repositories {
    maven { url 'https://repo.eclipse.org/content/repositories/paho-snapshots/' }
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    final PLAY_SERVICES_VERSION = '9.4.0'
    final SUPPORT_LIBRARY_VERSION = '24.1.1'
    final DAGGER_VERSION = '2.6'
    final OKHTTP_VERSION = '3.4.1'


    compile files('jniLibs/kalium-jni-1.0.0-SNAPSHOT.jar', )

    // Support libraries
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"

    // Play Services libraries
    compile "com.google.android.gms:play-services-gcm:$PLAY_SERVICES_VERSION"
    compile "com.google.android.gms:play-services-maps:$PLAY_SERVICES_VERSION"
    compile "com.google.android.gms:play-services-location:$PLAY_SERVICES_VERSION"
    compile "com.google.android.gms:play-services-places:$PLAY_SERVICES_VERSION"
    compile "com.google.android.gms:play-services-base:$PLAY_SERVICES_VERSION"


    // Utility libraries
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'org.greenrobot:greendao:3.0.1'
    compile 'org.altbeacon:android-beacon-library:2.7.1@aar'
    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.1-SNAPSHOT'
    compile 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1-20160623.113843-1'

    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"

    compile 'com.fasterxml.jackson.core:jackson-core:2.7.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
    compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter:1.1.0'
    compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter-recyclerview:1.1.0'
    compile 'com.jakewharton.timber:timber:4.1.2'

    // Widget libraries
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile('com.mikepenz:fastadapter:1.1.2@aar') {
        transitive = true
    }
    compile 'com.mikepenz:materialdrawer:5.0.5@aar'
    compile 'com.mikepenz:materialize:0.8.8@aar'
    compile 'com.mikepenz:iconics-core:2.5.11@aar'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.8@aar') {
        transitive = true
    }
    compile('com.github.afollestad.material-dialogs:commons:0.8.5.8@aar') {
        transitive = true
    }
    compile 'com.joanzapata.android:android-iconify:1.0.10'
    compile 'com.obsez.android.lib.filechooser:filechooser:1.1.2'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

}

greendao {
    schemaVersion 15
}